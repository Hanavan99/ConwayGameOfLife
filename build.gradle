apply plugin: "java"
apply plugin: "eclipse"

sourceSets {
	deploy
	applet
	ui
}

repositories {
	mavenCentral()
}

dependencies {
	compile "org.apache.logging.log4j:log4j-api:2.5"
	runtime "org.apache.logging.log4j:log4j-core:2.5"
	uiCompile files("lib/GUILogger.jar")
	uiCompile "org.apache.logging.log4j:log4j-api:2.5"
	uiCompile "org.apache.logging.log4j:log4j-core:2.5"
	deployCompile "com.sparkjava:spark-core:2.2"
	testCompile "junit:junit:4.12"
	appletCompile sourceSets.ui.output
	appletCompile sourceSets.main.output
	uiCompile sourceSets.main.output
}

configurations {
	eclipseCP {
		dependencies {
			compile files("lib/GUILogger.jar")
			compile "com.sparkjava:spark-core:2.2"
		}
	}
	appletDeploy {
		dependencies {
			compile files("lib/GUILogger.jar")
			compile "org.apache.logging.log4j:log4j-api:2.5"
			compile "org.apache.logging.log4j:log4j-core:2.5"
		}
	}
}

eclipse {
	classpath {
		downloadSources = true
		downloadJavadoc = true
		plusConfigurations += configurations.eclipseCP
	}
}

task run(type: JavaExec) {
	main = "com.github.hanavan99.conwaygameoflife.main.Main"
	classpath = sourceSets.ui.runtimeClasspath
	classpath sourceSets.main.runtimeClasspath
	dependsOn classes
	dependsOn uiClasses
}

task runServer(type: JavaExec) {
	main = "com.github.hanavan99.conwaygameoflife.main.Main"
	classpath = sourceSets.main.runtimeClasspath
	dependsOn classes
}

task deployJar(type: Jar) {
	from files(sourceSets.deploy.output.classesDir)
	from files(sourceSets.deploy.output.resourcesDir)
	from {
		configurations.deployCompile.collect {
			zipTree(it)
		}
	}
	exclude "META-INF/*"
	manifest {
		attributes "Main-Class": "com.github.hanavan99.conwaygameoflife.deploy.Main"
	}
	baseName = "deploy"
	dependsOn deployClasses
	dependsOn jar
}

task unsignedAppletJar(type: Jar) {
	from files(sourceSets.ui.output.classesDir)
	from files(sourceSets.applet.output.classesDir)
	from files(sourceSets.applet.output.resourcesDir)
	from {
		configurations.appletDeploy.collect {
			zipTree(it)
		}
	}
	exclude "META-INF/*"
	manifest {
		attributes "Permissions": "sandbox",
			"Codebase": "hanavan99.github.io",
			"Application-Name": "Conway's Game of Life"
	}
	baseName = "unsignedApplet"
	dependsOn uiClasses
	dependsOn appletClasses
}

task appletJar {
	AntBuilder ant = getAnt()
	File keystore = new File("deploy.jks")
	File unsignedJar = unsignedAppletJar.archivePath
	File signedJar = new File(unsignedJar.getParentFile(), "applet.jar")
	getInputs().file(unsignedJar)
	getOutputs().file(signedJar).upToDateWhen {
		return unsignedJar.exists() && signedJar.exists() && unsignedJar.lastModified() < signedJar.lastModified()
	}
	doLast {
		ant.signjar(
			jar: unsignedJar.getAbsolutePath(),
			alias: "deploy",
			keystore: keystore.getAbsolutePath(),
			storepass: System.getenv("KEYSTOREPASS"),
			signedjar: signedJar.getAbsolutePath()
		)
	}
	dependsOn unsignedAppletJar
}

task serverJar(type: Jar) {
	from files(sourceSets.main.output.classesDir)
	from files(sourceSets.main.output.resourcesDir)
	manifest {
		attributes "Main-Class": "com.github.hanavan99.conwaygameoflife.main.Main"
	}
	baseName = "server"
	dependsOn classes
}

jar {
	from files(sourceSets.ui.output.classesDir)
	from files(sourceSets.ui.output.resourcesDir)
	manifest {
		attributes "Main-Class": "com.github.hanavan99.conwaygameoflife.main.Main"
	}
	baseName = "client"
	dependsOn classes
	dependsOn uiClasses
}

task stage {
	dependsOn deployJar
}

build {
	dependsOn stage
	dependsOn unsignedAppletJar
	dependsOn serverJar
	if ( new File("deploy.jks").exists() ) {
		dependsOn appletJar
	}
}
